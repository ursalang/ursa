// Ursa grammar.
// Â© Reuben Thomas 2023-2025
// Released under the MIT license.

Ursa {
  Sequence = ListOf<Statement, #sc> #sc

  // A semicolon is "automatically inserted" at the end of the input stream,
  // or if a line terminator or "}" token causes parsing to fail.
  // NOTE: Applications of this rule *must* appear in a lexical context: either in the body of a
  // lexical rule, or inside `#()`.
  sc = space* (";" | end)
     | spacesNoNL (lineTerminator | &"}")

  PrimaryExp
    = null              -- null
    | bool
    | string
    | literalString
    | number
    | Map
    | Object
    | ident             -- ident
    | List
    | Block
    | Fn
    | "(" Exp ")"       -- paren

  Definition = ident Initializer
  Initializer = "=" Exp

  List (a list) = "[" ListOf<Exp, ","> ","? "]"

  Object (an object) = NamedType? "{" ListOf<Definition, #sc> #sc "}"

  Map (a map) = "{" ListOf<KeyValue, ","> ","? "}"
  KeyValue = Exp ":" Exp

  PostfixExp
    = PostfixExp "." ident #spacesNoNL #"(" ListOf<Exp, ","> ","? ")" -- invoke
    | PostfixExp "." ident                                            -- property
    | PostfixExp #spacesNoNL #"(" ListOf<Exp, ","> ","? ")"           -- call
    | PrimaryExp                                                      -- primary

  Ifs = NonemptyListOf<If, else> (else Block)?
  If = if Exp Block

  Fn (a function) = FnType Block
  FnType (a function type) = (fn | gen) "(" ListOf<Param, ","> ","? ")" TypeAnnotation
  Param = ident TypeAnnotation

  Loop = loop Block

  For = for ident in Exp Block

  UnaryExp
    = "~" UnaryExp    -- bitwise_not
    | "+" UnaryExp    -- pos
    | "-" UnaryExp    -- neg
    | PostfixExp

  ExponentExp
    = UnaryExp "**" ExponentExp  -- power
    | UnaryExp

  ProductExp
    = ProductExp "*" ExponentExp  -- times
    | ProductExp "/" ExponentExp  -- divide
    | ProductExp "%" ExponentExp  -- mod
    | ExponentExp

  SumExp
    = SumExp "+" ProductExp  -- plus
    | SumExp "-" ProductExp  -- minus
    | ProductExp

  CompareExp
    = CompareExp "==" SumExp -- eq
    | CompareExp "!=" SumExp -- neq
    | CompareExp "<"  SumExp -- lt
    | CompareExp "<=" SumExp -- leq
    | CompareExp ">"  SumExp -- gt
    | CompareExp ">=" SumExp -- geq
    | SumExp

  BitwiseExp
    = BitwiseExp "&" CompareExp   -- and
    | BitwiseExp "^" CompareExp   -- xor
    | BitwiseExp "|" CompareExp   -- or
    | BitwiseExp "<<" CompareExp  -- lshift
    | BitwiseExp ">>" CompareExp  -- arshift
    | BitwiseExp ">>>" CompareExp -- lrshift
    | CompareExp

  LogicNotExp
    = not BitwiseExp          -- not
    | BitwiseExp

  LogicExp
    = LogicExp and LogicNotExp -- and
    | LogicExp or LogicNotExp  -- or
    | LogicNotExp

  Exp
    = Ifs
    | Loop
    | For
    | await Exp       -- await
    | yield Exp?      -- yield
    | launch Exp      -- launch
    | LogicExp

  Assignment
    = PostfixExp ":=" Exp -- ass

  Statement
    = Assignment
    | Lets
    | Use
    | break Exp?      -- break
    | continue        -- continue
    | return Exp?     -- return
    | Exp

  Lets = NonemptyListOf<Let, and>
  Let = (let|var) Definition

  Use = use Path

  Block (a block) = "{" Sequence "}"

  // Types
  // TODO: Add namespace support: NamedType = Path TypeArgs?
  NamedType = ident TypeArgs?
  Type = NamedType | FnType
  TypeParams = "<" ListOf<TypeParam, ","> ","? ">"
  TypeParam = ident TypeAnnotation
  TypeArgs
    = "<" ListOf<Type, ","> ","? ">"
  NamedTypeAnnotation = ":" NamedType
  TypeAnnotation = ":" Type
  FnTypeAnnotation = ":" FnType

  // Classes
  Class = class ident TypeParams NamedTypeAnnotation "{" ListOf<ClassMember, #sc> #sc "}"
  ClassMember = ClassField | ClassMethod
  ClassField = pub? static? var? ident TypeAnnotation? Initializer?
  ClassMethod = pub? static? ident TypeParams? "=" Fn

  // Traits
  Trait = trait ident TypeParams NamedTypeAnnotation "{" ListOf<TraitMember, #sc> #sc "}"
  TraitMember = TraitField | TraitMethod
  TraitField = var? ident TypeAnnotation?
  TraitMethod = ident TypeParams? FnTypeAnnotation

  // Convenience rules for parsing keyword tokens.
  and = "and" ~identifierPart
  await = "await" ~identifierPart
  break = "break" ~identifierPart
  class = "class" ~identifierPart
  continue = "continue" ~identifierPart
  else = "else" ~identifierPart
  fn = "fn" ~identifierPart
  for = "for" ~identifierPart
  gen = "gen" ~identifierPart
  if = "if" ~identifierPart
  in = "in" ~identifierPart
  launch = "launch" ~identifierPart
  let = "let" ~identifierPart
  loop = "loop" ~identifierPart
  not = "not" ~identifierPart
  or = "or" ~identifierPart
  pub = "pub" ~identifierPart
  return = "return" ~identifierPart
  static = "static" ~identifierPart
  trait = "trait" ~identifierPart
  use = "use" ~identifierPart
  var = "var" ~identifierPart
  yield = "yield" ~identifierPart

  keyword = and | await | break | class | continue | else | fn | for | gen | if
    | in | launch | let | loop | not | or | pub | return | static | trait
    | use | var | yield

  reservedWord = bool | null | keyword

  // Make ident indirect so we can easily get a node with the whole identifier.
  ident (an identifier) = identName
  identName = ~reservedWord identifierStart identifierPart*
  identifierStart = letter | "_"
  identifierPart = identifierStart | digit

  Path = NonemptyListOf<ident, ".">

  null = "null" ~identifierPart

  bool (a boolean) = ("false" | "true") ~identifierPart

  number (a number)
    = digit* "." digit+  -- fract
    | digit+             -- whole

  string (a string) = "\"" stringCharacter* "\""
  stringCharacter
    = ~("\"" | "\\" | lineTerminator) any     -- nonEscaped
    | "\\" escapeSequence    -- escaped
    | lineContinuation       -- lineContinuation
  lineContinuation = "\\" lineTerminatorSequence
  escapeSequence
    = unicodeEscapeSequence
    | hexEscapeSequence
    | characterEscapeSequence  // Must come last.
  characterEscapeSequence
    = singleEscapeCharacter
    | nonEscapeCharacter
  singleEscapeCharacter =
    "\"" | "\\" | "b" | "f" | "n" | "r" | "t" | "v"
  nonEscapeCharacter
    = ~(escapeCharacter | lineTerminator) any
  escapeCharacter
    = singleEscapeCharacter | digit | "x" | "u"
  hexEscapeSequence
    = "x" hexDigit hexDigit
  unicodeEscapeSequence
    = "u" hexDigit hexDigit hexDigit hexDigit
  lineTerminator (a line terminator)
    = "\n" | "\r" | "\x0B" | "\x0C" | "\u2028" | "\u2029"
  lineTerminatorSequence
    = "\n" | "\r" ~"\n" | "\x0B" | "\x0C" | "\u2028" | "\u2029" | "\r\n"

  literalString (a literal string)
    = "r\"" (~"\"" any)* "\""
    | "r#\"" (~"\"#" any)* "\"#"
    | "r##\"" (~"\"##" any)* "\"##"
    | "r###\"" (~"\"###" any)* "\"###"
    | "r####\"" (~"\"####" any)* "\"####"

  comment (a comment) = blockComment | lineComment
  blockComment = "/*" (~("*/" | "/*") any)* blockComment? (~"*/" any)* "*/"
  lineComment = "//" (~lineTerminator any)*

  // Override Ohm's built-in definition
  space := whitespace | lineTerminator | comment

  // Does not accept lineTerminators
  spacesNoNL = (whitespace | comment)*

  whitespace (whitespace)
    = "\t"
    | " "
    | "\u00A0"  -- noBreakSpace
    | "\uFEFF"  -- byteOrderMark
    | unicodeSpaceSeparator

  unicodeSpaceSeparator = "\u2000".."\u200B" | "\u3000"
}
